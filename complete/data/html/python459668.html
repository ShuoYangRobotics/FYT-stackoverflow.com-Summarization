<h3>Question (ID-459668):</h3><h2>Is there a builtin "hash to string" in Perl?</h2><p>I'm coming to learn Perl from a Python background where the following hash-to-string conversion is built in to the language:</p>

<pre><code>&gt;&gt;&gt; d = {'a': 1, 'b': 2, 'c': 3}
&gt;&gt;&gt; str(d)
"{'a': 1, 'c': 3, 'b': 2}"
</code></pre>

<p>Is there a builtin and/or module that has a subroutine with output along the lines of:</p>

<pre><code>"('a' =&gt; 1, 'b' =&gt; 2, 'c' =&gt; 3)"
</code></pre>

<p>Strangely, a web search for <code>perl "hash to string"</code> doesn't turn up anything along the lines I'm looking for. Thanks!</p>
<br /><h3>Answers (Total-6):</h3><b>#0</b><br /><pre><code>use Data::Dumper;
local $Data::Dumper::Terse = 1;
my $str = Dumper({a =&gt; 1, b =&gt; 2, c =&gt; 3});
</code></pre>
<br /><b>#1</b><br /><p>See also <a href="http://search.cpan.org/~makamaka/JSON-2.12/lib/JSON.pm" rel="nofollow">JSON</a>:</p>

<pre><code>#!/usr/bin/perl
use warnings; use strict;
use JSON;

my $data = {a =&gt; 1, b=&gt; 2, c =&gt; 3};

print to_json($data);
</code></pre>

<p>This produces:</p>

<pre><code>{"c":3,"a":1,"b":2}
</code></pre>
<br /><b>#2</b><br /><p>There is the <a href="http://search.cpan.org/dist/Data-Dumper/" rel="nofollow"><code>Data::Dumper</code></a> module which one way to do this sort of transformation.</p>
<br /><b>#3</b><br /><p>Use <a href="http://search.cpan.org/dist/Data-Dump/" rel="nofollow">Data::Dump</a> instead of Data::Dumper.  It's basically the same, except without that annoying <code>$VAR1 = ...</code> cruft:</p>

<pre><code>use Data::Dump "pp";
print pp({a =&gt; 1, b =&gt; 2, c =&gt; 3});
</code></pre>

<p>Produces:</p>

<pre><code>{ a =&gt; 1, b =&gt; 2, c =&gt; 3 }
</code></pre>

<p>If you're on Windows, Data::Dump has come pre-installed with <a href="http://aspn.activestate.com/ASPN/Downloads/ActivePerl/" rel="nofollow">ActivePerl</a> since version 5.8.</p>
<br /><b>#4</b><br /><p>Yet Another Swallow Solution:</p>

<pre><code>sub pp {
  my $h = shift();
  qq[{${\(join',',map"$_=&gt;$h-&gt;{$_}",keys%$h)}}]
}
print pp({a =&gt; 1, b =&gt; 2, c =&gt; 3});
</code></pre>

<p>But use <a href="http://search.cpan.org/dist/Data-Dumper/Dumper.pm" rel="nofollow"><code>Data::Dumper</code></a> instead.</p>

<p>For very fancy output you can use also:</p>

<pre><code>use Data::Dumper;
use Perl::Tidy;
sub pp {
        local $Data::Dumper::Terse    = 1;
        local $Data::Dumper::Indent   = 0;
        my $source = Dumper(@_);
        my $result;
        Perl::Tidy::perltidy(
                source      =&gt; \$source,
                destination =&gt; \$result,
                argv        =&gt; [qw(-pbp -nst)]
        );
        return $result;
}
</code></pre>

<p>If you prefer some keys should be first than you can use this approach (i want <code>type</code> first and <code>position</code> second):</p>

<pre><code>    local $Data::Dumper::Sortkeys = sub {
            [   sort {
                            if    ( $b eq 'type' )     {1}
                            elsif ( $a eq 'type' )     {-1}
                            elsif ( $b eq 'position' ) {1}
                            elsif ( $a eq 'position' ) {-1}
                            else                       { $a cmp $b }
                            } keys %{ $_[0] }
            ];
    };
</code></pre>
<br /><b>#5</b><br /><p>Several of the above solutions have a problem if you have the potential for multi-level structures.</p>

<p>Specifically this flag:</p>

<pre><code>$Data::Dumper::Terse    = 1;
</code></pre>

<p>As noted on the perldoc page for Data::Dumper, the "terse" flag could general not perl parseable output.</p>

<p>If you possibly are going to have multi-depth structures the proper thing to do would be to instead use:</p>

<pre><code>$Data::Dumper::Indent = 0;
</code></pre>

<p>Which is guaranteed to be perl parseable by eval, which makes for a very very easy way of doing serialization to plaintext...</p>
<br />