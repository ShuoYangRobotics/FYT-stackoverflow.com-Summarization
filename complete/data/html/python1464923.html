<h3>Question (ID-1464923):</h3><h2>How can I print only every third index in Perl or Python?</h2><p>How can I do a for() or foreach() loop in Python and Perl, respectively, that only prints every third index? I need to move every third index to a new array.</p>
<br /><h3>Answers (Total-8):</h3><b>#0</b><br /><p>Python</p>

<pre><code>print list[::3] # print it
newlist = list[::3] # copy it
</code></pre>

<p>Perl</p>

<pre><code>for ($i = 0; $i &lt; @list; $i += 3) {
    print $list[$i]; # print it
    push @y, $list[$i]; # copy it
}
</code></pre>
<br /><b>#1</b><br /><p><strong>Perl:</strong></p>

<p>As with draegtun's answer, but using a count var:</p>

<pre><code>my $i;
my @new = grep {not ++$i % 3} @list;
</code></pre>
<br /><b>#2</b><br /><p>Perl 5.10 new <a href="http://perldoc.perl.org/functions/state.html" rel="nofollow">state</a> variables comes in very handy here:</p>

<pre><code>my @every_third = grep { state $n = 0; ++$n % 3 == 0 } @list;
</code></pre>

<p><br>
Also note you can provide a list of elements to slice:</p>

<pre><code>my @every_third = @list[ 2, 5, 8 ];  # returns 3rd, 5th &amp; 9th items in list
</code></pre>

<p>You can dynamically create this slice list using <a href="http://perldoc.perl.org/functions/map.html" rel="nofollow">map</a> (see Gugod's excellent <a href="http://stackoverflow.com/questions/1464923/how-can-i-print-only-every-third-index-in-perl-or-python/1466696#1466696">answer</a>) or a subroutine: </p>

<pre><code>my @every_third = @list[ loop( start =&gt; 2, upto =&gt; $#list, by =&gt; 3  ) ];

sub loop {
    my ( %p ) = @_;
    my @list;

    for ( my $i = $p{start} || 0; $i &lt;= $p{upto}; $i += $p{by} ) {
        push @list, $i;
    }

    return @list;
}
</code></pre>

<p><br></p>

<p><strong>Update:</strong></p>

<p>Regarding runrig's comment... this is "one way" to make it work within a loop:</p>

<pre><code>my @every_third = sub { grep { state $n = 0; ++$n % 3 == 0 } @list }-&gt;();
</code></pre>

<p>/I3az/</p>
<br /><b>#3</b><br /><p>Python:</p>

<pre><code>for x in a[::3]:
   something(x)
</code></pre>
<br /><b>#4</b><br /><p>Perl:</p>

<pre><code># The initial array
my @a = (1..100);

# Copy it, every 3rd elements
my @b = @a[ map { 3 * $_ } 0..$#a/3 ];

# Print it. space-delimited
$, = " ";
say @b;
</code></pre>
<br /><b>#5</b><br /><p>You could do a slice in Perl.</p>

<pre><code>my @in = ( 1..10 );

# need only 1/3 as many indexes.
my @index = 1..(@in/3);

# adjust the indexes.
$_ = 3 * $_ - 1 for @index;
# These would also work
# $_ *= 3, --$_ for @index;
# --($_ *= 3) for @index

my @out = @in[@index];
</code></pre>
<br /><b>#6</b><br /><p>In Perl:</p>

<pre><code>$size = @array; 
for ($i=0; $i&lt;$size; $i+=3)  # or start from $i=2, depends what you mean by "every third index"
{  
        print "$array[$i] ";  
}
</code></pre>
<br /><b>#7</b><br /><pre>
@array = qw(1 2 3 4 5 6 7 8 9);
print @array[(grep { ($_ + 1) % 3 == 0 } (1..$#array))];
</pre>
<br />