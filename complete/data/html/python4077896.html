<h3>Question (ID-4077896):</h3><h2>Perl or Python: Convert date from dd/mm/yyyy to yyyy-mm-dd</h2><p>I have lots of dates in a column in a CSV file that I need to convert from dd/mm/yyyy to yyyy-mm-dd format.  For example 17/01/2010 should be converted to 2010-01-17. </p>

<p>How can I do this in Perl or Python?</p>
<br /><h3>Answers (Total-9):</h3><b>#0</b><br /><p>If you are guaranteed to have well-formed data consisting of nothing else but a singleton date in the DD-MM-YYYY format, then this works:</p>

<pre><code># FIRST METHOD
my $ndate = join("-" =&gt; reverse split(m[/], $date));
</code></pre>

<p>That works on a <code>$date</code> holding "07/04/1776" but fails on "this 17/01/2010 and that 01/17/2010 there".  Instead, use:</p>

<pre><code># SECOND METHOD
($ndate = $date) =~ s{
    \b
      ( \d \d   )
    / ( \d \d   )
    / ( \d {4}  )
    \b
}{$3-$2-$1}gx;
</code></pre>

<p>If you prefer a more "grammatical" regex, so that itâ€™s easier to maintain and update, you can instead use this:</p>

<pre><code># THIRD METHOD
($ndate = $date) =~ s{
    (?&amp;break)

              (?&lt;DAY&gt;    (?&amp;day)    )
    (?&amp;slash) (?&lt;MONTH&gt;  (?&amp;month)  )
    (?&amp;slash) (?&lt;YEAR&gt;   (?&amp;year)   )

    (?&amp;break)

    (?(DEFINE)
        (?&lt;break&gt; \b     )
        (?&lt;slash&gt; /      )
        (?&lt;year&gt;  \d {4} )
        (?&lt;month&gt; \d {2} )
        (?&lt;day&gt;   \d {2} )
    )
}{
    join "-" =&gt; @+{qw&lt;YEAR MONTH DAY&gt;}
}gxe;
</code></pre>

<p>Finally, if you have Unicode data, you might want to be a bit more careful.</p>

<pre><code># FOURTH METHOD
($ndate = $date) =~ s{
    (?&amp;break_before)
              (?&lt;DAY&gt;    (?&amp;day)    )
    (?&amp;slash) (?&lt;MONTH&gt;  (?&amp;month)  )
    (?&amp;slash) (?&lt;YEAR&gt;   (?&amp;year)   )
    (?&amp;break_after)

    (?(DEFINE)
        (?&lt;slash&gt;     /                  )
        (?&lt;start&gt;     \A                 )
        (?&lt;finish&gt;    \z                 )

        # don't really want to use \D or [^0-9] here:
        (?&lt;break_before&gt;
           (?&lt;= [\pC\pP\pS\p{Space}] )
         | (?&lt;= \A                )
        )
        (?&lt;break_after&gt;
            (?= [\pC\pP\pS\p{Space}]
              | \z
            )
        )
        (?&lt;digit&gt; \d            )
        (?&lt;year&gt;  (?&amp;digit) {4} )
        (?&lt;month&gt; (?&amp;digit) {2} )
        (?&lt;day&gt;   (?&amp;digit) {2} )
    )
}{
    join "-" =&gt; @+{qw&lt;YEAR MONTH DAY&gt;}
}gxe;
</code></pre>

<p>You can see how each of these four approaches performs when confronted with sample input strings like these:</p>

<pre><code>my $sample  = q(17/01/2010);
my @strings =  (
    $sample,  # trivial case

    # multiple case
    "this $sample and that $sample there",

    # multiple case with non-ASCII BMP code points
    # U+201C and U+201D are LEFT and RIGHT DOUBLE QUOTATION MARK
    "from \x{201c}$sample\x{201d} through\xA0$sample",

    # multiple case with non-ASCII code points
    #   from both the BMP and the SMP 
    # code point U+02013 is EN DASH, props \pP \p{Pd}
    # code point U+10179 is GREEK YEAR SIGN, props \pS \p{So}
    # code point U+110BD is KAITHI NUMBER SIGN, props \pC \p{Cf}
    "\x{10179}$sample\x{2013}\x{110BD}$sample",
);
</code></pre>

<p>Now letting <code>$date</code> be a <code>foreach</code> iterator through that array, we get this output:</p>

<pre><code>Original is:   17/01/2010
First method:  2010-01-17
Second method: 2010-01-17
Third method:  2010-01-17
Fourth method: 2010-01-17

Original is:   this 17/01/2010 and that 17/01/2010 there
First method:  2010 there-01-2010 and that 17-01-this 17
Second method: this 2010-01-17 and that 2010-01-17 there
Third method:  this 2010-01-17 and that 2010-01-17 there
Fourth method: this 2010-01-17 and that 2010-01-17 there

Original is:   from â€œ17/01/2010â€ throughÂ 17/01/2010
First method:  2010-01-2010â€ throughÂ 17-01-from â€œ17
Second method: from â€œ2010-01-17â€ throughÂ 2010-01-17
Third method:  from â€œ2010-01-17â€ throughÂ 2010-01-17
Fourth method: from â€œ2010-01-17â€ throughÂ 2010-01-17

Original is:   ğ…¹17/01/2010â€“ğ‘‚½17/01/2010
First method:  2010-01-2010â€“ğ‘‚½17-01-ğ…¹17
Second method: ğ…¹2010-01-17â€“ğ‘‚½2010-01-17
Third method:  ğ…¹2010-01-17â€“ğ‘‚½2010-01-17
Fourth method: ğ…¹2010-01-17â€“ğ‘‚½2010-01-17
</code></pre>

<p>Now letâ€™s suppose that you actually <em>do</em> want to match non-ASCII digits.   For example:</p>

<pre><code>   U+660  ARABIC-INDIC DIGIT ZERO
   U+661  ARABIC-INDIC DIGIT ONE
   U+662  ARABIC-INDIC DIGIT TWO
   U+663  ARABIC-INDIC DIGIT THREE
   U+664  ARABIC-INDIC DIGIT FOUR
   U+665  ARABIC-INDIC DIGIT FIVE
   U+666  ARABIC-INDIC DIGIT SIX
   U+667  ARABIC-INDIC DIGIT SEVEN
   U+668  ARABIC-INDIC DIGIT EIGHT
   U+669  ARABIC-INDIC DIGIT NINE
</code></pre>

<p>or even </p>

<pre><code> U+1D7F6  MATHEMATICAL MONOSPACE DIGIT ZERO
 U+1D7F7  MATHEMATICAL MONOSPACE DIGIT ONE
 U+1D7F8  MATHEMATICAL MONOSPACE DIGIT TWO
 U+1D7F9  MATHEMATICAL MONOSPACE DIGIT THREE
 U+1D7FA  MATHEMATICAL MONOSPACE DIGIT FOUR
 U+1D7FB  MATHEMATICAL MONOSPACE DIGIT FIVE
 U+1D7FC  MATHEMATICAL MONOSPACE DIGIT SIX
 U+1D7FD  MATHEMATICAL MONOSPACE DIGIT SEVEN
 U+1D7FE  MATHEMATICAL MONOSPACE DIGIT EIGHT
 U+1D7FF  MATHEMATICAL MONOSPACE DIGIT NINE
</code></pre>

<p>So imagine you have a date in mathematical monospace digits, like this:</p>

<pre><code>$date = "\x{1D7F7}\x{1D7FD}/\x{1D7F7}\x{1D7F6}/\x{1D7F8}\x{1D7F6}\x{1D7F7}\x{1D7F6}";
</code></pre>

<p>The Perl code will work just fine on that:</p>

<pre><code>Original is:   ğŸ·ğŸ½/ğŸ·ğŸ¶/ğŸ¸ğŸ¶ğŸ·ğŸ¶
First method:  ğŸ¸ğŸ¶ğŸ·ğŸ¶-ğŸ·ğŸ¶-ğŸ·ğŸ½
Second method: ğŸ¸ğŸ¶ğŸ·ğŸ¶-ğŸ·ğŸ¶-ğŸ·ğŸ½
Third method:  ğŸ¸ğŸ¶ğŸ·ğŸ¶-ğŸ·ğŸ¶-ğŸ·ğŸ½
Fourth method: ğŸ¸ğŸ¶ğŸ·ğŸ¶-ğŸ·ğŸ¶-ğŸ·ğŸ½
</code></pre>

<p>I think youâ€™ll find that Python has a pretty brainâ€damaged Unicode model whose lack of support for abstract characters and strings irrespective of content makes it ridiculously difficult to write things like this.  </p>

<p>Itâ€™s also tough to write legible regular expressions in Python where you decouple the declaration of the subexpressions from their execution, since <code>(?(DEFINE)...)</code> blocks are not supported there.  Heck, Python doesnâ€™t even support Unicode properties.  Itâ€™s just not suitable for Unicode regex work because of this.</p>

<p>But hey, if you think thatâ€™s bad in Python compared to Perl (<em>and it certainly is</em>), just try any other language.  I havenâ€™t found one that isnâ€™t still worse for this sort of work.</p>

<p>As you see, you run into real problems when you ask for regex solutions from multiple languages.  First of all, the solutions are difficult to compare because of the different regex flavors.  But also because no other language can compare with Perl for power, expressivity, and maintainability in its regular expressions. This may become even more obvious once arbitrary Unicode enters the picture.</p>

<p>So if you just wanted Python, you should have asked for only that.  Otherwise itâ€™s a terribly unfair contest that Python will nearly always lose; itâ€™s just too messy to get things like this correct in Python, let alone <em>both</em> correct <strong>and clean</strong>.  Thatâ€™s asking more of it than it can produce.</p>

<p>In contrast, Perlâ€™s regexes excel at both those.</p>
<br /><b>#1</b><br /><pre><code>&gt;&gt;&gt; from datetime import datetime
&gt;&gt;&gt; datetime.strptime('02/11/2010', '%d/%m/%Y').strftime('%Y-%m-%d')
'2010-11-02'
</code></pre>

<p>or more hackish way (that doesn't check for validity of values):</p>

<pre><code>&gt;&gt;&gt; '-'.join('02/11/2010'.split('/')[::-1])
'2010-11-02'
&gt;&gt;&gt; '-'.join(reversed('02/11/2010'.split('/')))
'2010-11-02'
</code></pre>
<br /><b>#2</b><br /><p>Use Time::Piece (in core since 5.9.5), very similar to the Python solution accepted, as it provides the strptime and strftime functions:</p>

<pre><code>use Time::Piece;
my $dt_str = Time::Piece-&gt;strptime('13/10/1979', '%d/%m/%Y')-&gt;strftime('%Y-%m-%d');
</code></pre>

<p>or</p>

<pre><code>$ perl -MTime::Piece
print Time::Piece-&gt;strptime('13/10/1979', '%d/%m/%Y')-&gt;strftime('%Y-%m-%d');
1979-10-13
$ 
</code></pre>
<br /><b>#3</b><br /><p>Go with Perl: the <code>datetime</code> Python package is just broken. You could just do it with regexes to swap the date parts around, eg</p>

<pre><code>echo "17/01/2010" | perl -pe 's{(\d+)/(\d+)/(\d+)}{$3-$2-$1}g'
</code></pre>

<p>If you do need to parse these dates (eg to compute their day of week or other calendar-type operations), look into <a href="http://search.cpan.org/~rokr/DateTimeX-Easy-0.089/lib/DateTimeX/Easy.pm" rel="nofollow">DateTimeX::Easy</a> (you can install it with <code>apt-get</code> under Ubuntu):</p>

<pre><code>perl -MDateTimeX::Easy -e 'print DateTimeX::Easy-&gt;parse("17/01/2010")-&gt;ymd("-")'
</code></pre>
<br /><b>#4</b><br /><p>Perl :</p>

<pre><code>while (&lt;&gt;) {
  s/(^|[^\d])(\d\d)\/(\d\d)\/(\d{4})($|[^\d])/$4-$3-$2/g;
  print $_;
}
</code></pre>

<p>Then you just have to run:</p>

<pre><code>perl MyScript.pl &lt; oldfile.txt &gt; newfile.txt
</code></pre>
<br /><b>#5</b><br /><p>Perl: </p>

<pre><code>my $date =~ s/(\d+)\/(\d+)\/(\d+)/$3-$2-$1/;
</code></pre>
<br /><b>#6</b><br /><p>In Perl you can do:</p>

<pre><code>use strict;
while(&lt;&gt;) {
    chomp;
    my($d,$m,$y) = split/\//;
    my $newDate = $y.'-'.$m.'-'.$d;
}
</code></pre>
<br /><b>#7</b><br /><p>using python</p>

<pre><code>import string
before = "17/01/2010"
array = string.split(before,"/")
after = array[2] + '-' + array[1] + '-' + array[0]
print after
</code></pre>
<br /><b>#8</b><br /><p>In glorious perl-oneliner form:</p>

<pre><code>echo 17/01/2010 | perl -p -e "chomp;  join('-', reverse split /\//);"
</code></pre>

<p>But seriously I would do it like this:</p>

<pre><code>#!/usr/bin/env perl
while (&lt;&gt;) {
    chomp;
    print join('-', reverse split /\//), "\n";
}
</code></pre>

<p>Which will work on a pipe, converting and printing one date per line. </p>
<br />