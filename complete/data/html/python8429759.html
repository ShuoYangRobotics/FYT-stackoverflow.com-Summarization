<h3>Question (ID-8429759):</h3><h2>extracting specific data from a table</h2><p>I have table which looks like this (tab separated):</p>

<pre><code>Ron  Rob  rock bammy
m    f   m  f
florida  Atlanta  florida texas 
</code></pre>

<p>This table is of order 5*512 and based on row 3 data, I want to extract the values in row1.
 for example: I want to have names of all person living in florida and texas, in a table of 2 columns and n number of rows.</p>

<pre><code>Florida  Ron
Florida  Rock
Texas BAmmy
</code></pre>

<p>and so on.</p>

<p>any suggestions for a bash or PERL liners...</p>

<p>Thank you in advance.</p>
<br /><h3>Answers (Total-6):</h3><b>#0</b><br /><pre><code>awk 'NR==1{for(i=1;i&lt;=NF;i++)n[i]=$i}; NR==3{for(i=1;i&lt;=NF;i++){if($i~/florida|texas/)print $i"\t"n[i];}}' yourFile
</code></pre>

<p>see the test below:</p>

<pre><code>kent$  echo "Ron Rob rock bammy
m f m f
florida Atlanta florida texas"|awk 'NR==1{for(i=1;i&lt;=NF;i++)n[i]=$i}; NR==3{for(i=1;i&lt;=NF;i++){if($i~/florida|texas/)print $i"\t"n[i];}}'
</code></pre>

<p><strong>output</strong></p>

<pre><code>florida Ron
florida rock
texas   bammy
</code></pre>

<p><strong>EDIT</strong></p>

<pre><code>kent$  echo "Ron  Rob  rock bammy
m    f   m  f
florida(8)  Atlanta  florida(8) texas(2;7)"|awk 'NR==1{for(i=1;i&lt;=NF;i++)n[i]=$i}; NR==3{for(i=1;i&lt;=NF;i++){if($i~/florida\(8\)|texas\(2;7\)/)print $i"\t"n[i];}}'
</code></pre>

<p>output:</p>

<pre><code>florida(8)      Ron
florida(8)      rock
texas(2;7)      bammy
</code></pre>
<br /><b>#1</b><br /><p>Yet another Perl solution:</p>

<pre><code>perl -ane 'push@c,@F}{print grep{/^(florida|atlanta)\t/i}map{"$c[$_+$#c/3*2+1]\t$c[$_]\n"}0..$#c/3'
</code></pre>

<p>Or as a script</p>

<pre><code>#!/usr/bin/perl

use strict;
use warnings;

my (@data, @rows);

push @data, split/\s+/ while (&lt;&gt;);

for (0 .. $#data/3) {
    my $name = $data[$_];
    my $location = $data[$_+$#data/3*2+1];
    push @rows, "$location\t$name\n" if $location =~ /^(florida|atlanta)$/i;
}

print join("", @rows);
</code></pre>

<p>with an if condition inside the loop instead of the separate <code>grep</code>.</p>

<p>My approach is to flatten all three lines into a single array and use <code>for (0 .. $#data/3)</code> to loop over the indexes corresponding to the names from the first line and get the location from the matching column with <code>$data[$_+$#data/3*2+1]</code>.</p>
<br /><b>#2</b><br /><p>Try this in Python</p>

<pre><code>x="Ron &lt;t&gt; Rob &lt;t&gt; rock &lt;t&gt; bammy\nm   &lt;t&gt; f &lt;t&gt;  m &lt;t&gt; f\nflorida &lt;t&gt; Atlanta &lt;t&gt; florida &lt;t&gt; texas"
print '\n'.join(' &lt;t&gt; '.join([b,a]) for a,b in zip(x.splitlines()[0].split(),x.splitlines()[2].split()) if b in ['florida','texas'])

florida &lt;t&gt; Ron
florida &lt;t&gt; rock
texas &lt;t&gt; bammy
</code></pre>

<p>in-case you want to read from a file say data.txt you can do so as</p>

<pre><code>fin=open('data.txt')
data=fin.read()
</code></pre>

<p>then you can apply the above</p>

<pre><code>print '\n'.join(' &lt;t&gt; '.join([b,a]) for a,b in zip(data.splitlines()[0].split(),data.splitlines()[2].split()) if b in ['florida','texas'])
</code></pre>

<p>Note** Split the lines and Zip the 1st and the 3rd. Now for every tuple in the zipped list search for only those item where the second item in the tuple (i.e. country) is within the list ['florida','texas']</p>
<br /><b>#3</b><br /><p>Here's a Perl solution that works, but it's a bit more convoluted that I'd like. You'd probably be better off putting this data into a database.</p>

<pre><code>#!/usr/bin/env perl

use strict;
use warnings;
use 5.010;

my %rows = (
  name =&gt; 1,
  location =&gt; 3,
);

my %location = map { $_ =&gt; 1 } qw[florida texas];

my @names;

while (&lt;DATA&gt;) {
  next unless grep { $_ == $. } values %rows;

  chomp;

  if ($. == $rows{name}) {
    @names = split;
  }

  if ($. == $rows{location}) {
    my @locs = split;

    for my $x (0 .. $#locs) {
      if ($location{lc $locs[$x]}) {
        say ucfirst $locs[$x]. "\t$names[$x]";
      }
    }
    last;
  }
}

__END__
Ron     Rob     rock    bammy
m       f       m       f
florida         Atlanta florida texas
</code></pre>
<br /><b>#4</b><br /><pre><code>#!/usr/bin/env perl
use strict;
use warnings;

my $pat = shift;

sub interleave($$){
    my ($foo,$bar) = @_;
    return map { ( $_ , shift @{$bar} ) } @{$foo};
}

my $n=0;
my(@p,%h);
while(&lt;&gt;){
    chomp;
    if($n%3==0){
            @p = split /\t/, $_;
    } elsif($n%3==2){
            my @l = split /\t/, $_;
            my %kv = interleave(\@p, \@l);
            foreach my $k (keys %kv){
                    push(@{$h{$kv{$k}}}, $k);
            }
    }
    $n++;
}

foreach my $loc (keys %h){
    if(!defined $pat || $loc =~ /$pat/i){
            foreach my $name (@{$h{$loc}}){
                    print ucfirst($loc), "\t", ucfirst($name), "\n";
            }
    }
}
</code></pre>

<p>And then call it</p>

<pre><code>perl extract.pl 'texas|florida' &lt; data
</code></pre>

<p>"Oneliner" form:</p>

<pre><code>perl -ne 'BEGIN{$p=shift||"^";}chomp;if($n++%3!=1){unless(@p){@p=split/\t/,$_;next;}my %kv = map { ( $_ , shift @p ) } split(/\t/, $_);map { push(@{$h{$_}}, $kv{$_}); } keys %kv;}END{map{for my$nm(@{$h{$_}}){print ucfirst($_),"\t",ucfirst($nm),"\n";}}grep{/$p/i}keys%h;}' 'florida|texas' &lt; data
</code></pre>
<br /><b>#5</b><br /><p>Sounds to me like this is a job for <a href="http://search.cpan.org/perldoc?Text%3a%3aCSV_XS" rel="nofollow">Text::CSV_XS</a>. It is <strong>not</strong> a good idea to split on whitespace, as many seem to be suggesting, as that will fail for anything but simplified data.</p>

<p><strong>Code:</strong></p>

<pre><code>use strict;
use warnings;
use Text::CSV_XS;

my $csv = Text::CSV_XS-&gt;new( {
        sep_char    =&gt; "\t",
        binary      =&gt; 1,
    });

# get array refs to each row, with appropriate name
# For larger data sets, using an array to hold the array refs would be better
my $name       = $csv-&gt;getline(*DATA);
my $gender     = $csv-&gt;getline(*DATA);
my $city       = $csv-&gt;getline(*DATA);

for (keys @$city) {   # lists the column numbers
    if ($city-&gt;[$_] =~ /florida|texas/i) {
        print "$city-&gt;[$_]\t$name-&gt;[$_]\n";
    }
}

__DATA__
Ron Rob rock    bammy
m   f   m   f
florida Atlanta florida texas
</code></pre>

<p><strong>Output:</strong></p>

<pre><code>florida Ron
florida rock
texas   bammy
</code></pre>
<br />