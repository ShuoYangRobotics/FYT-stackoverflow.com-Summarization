Question (ID-1464923): How can I print only every third index in Perl or Python? How can I do a for() or foreach() loop in Python and Perl, respectively, that only prints every third index? I need to move every third index to a new array. 
 Answers (Total-8): #0 Python 

 print list[::3] # print it
newlist = list[::3] # copy it
 

 Perl 

 for ($i = 0; $i &lt; @list; $i += 3) {
 print $list[$i]; # print it
 push @y, $list[$i]; # copy it
}
 
 #1 Perl: 

 As with draegtun's answer, but using a count var: 

 my $i;
my @new = grep {not ++$i % 3} @list;
 
 #2 Perl 5.10 new state variables comes in very handy here: 

 my @every_third = grep { state $n = 0; ++$n % 3 == 0 } @list;
 

 
Also note you can provide a list of elements to slice: 

 my @every_third = @list[ 2, 5, 8 ]; # returns 3rd, 5th &amp; 9th items in list
 

 You can dynamically create this slice list using map (see Gugod's excellent answer ) or a subroutine: 

 my @every_third = @list[ loop( start =&gt; 2, upto =&gt; $#list, by =&gt; 3 ) ];

sub loop {
 my ( %p ) = @_;
 my @list;

 for ( my $i = $p{start} || 0; $i &lt;= $p{upto}; $i += $p{by} ) {
  push @list, $i;
 }

 return @list;
}
 

 

 Update: 

 Regarding runrig's comment... this is "one way" to make it work within a loop: 

 my @every_third = sub { grep { state $n = 0; ++$n % 3 == 0 } @list }-&gt;();
 

 /I3az/ 
 #3 Python: 

 for x in a[::3]:
 something(x)
 
 #4 Perl: 

 # The initial array
my @a = (1..100);

# Copy it, every 3rd elements
my @b = @a[ map { 3 * $_ } 0..$#a/3 ];

# Print it. space-delimited
$, = " ";
say @b;
 
 #5 You could do a slice in Perl. 

 my @in = ( 1..10 );

# need only 1/3 as many indexes.
my @index = 1..(@in/3);

# adjust the indexes.
$_ = 3 * $_ - 1 for @index;
# These would also work
# $_ *= 3, --$_ for @index;
# --($_ *= 3) for @index

my @out = @in[@index];
 
 #6 In Perl: 

 $size = @array; 
for ($i=0; $i&lt;$size; $i+=3) # or start from $i=2, depends what you mean by "every third index"
{ 
  print "$array[$i] "; 
}
 
 #7 
@array = qw(1 2 3 4 5 6 7 8 9);
print @array[(grep { ($_ + 1) % 3 == 0 } (1..$#array))];